// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Keyboard.jack

/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {


    // RAM[24576]

    static char key;

    /** Initializes the keyboard. */
    function void init() {

        return;
    } 

    /**
     * Returns the character of the currently pressed key on the keyboard;
     * if no key is currently pressed, returns 0.
     *
     * Recognizes all ASCII characters, as well as the following keys:
     * new line = 128 = String.newline()
     * backspace = 129 = String.backspace()
     * left arrow = 130
     * up arrow = 131
     * right arrow = 132
     * down arrow = 133
     * home = 134
     * End = 135
     * page up = 136
     * page down = 137
     * insert = 138
     * delete = 139
     * ESC = 140
     * F1 - F12 = 141 - 152
     */
    function char keyPressed() {
        return Memory.peek(24576);
    }

    /**								
     * Waits until a key is pressed on the keyboard and released,
     * then echoes the key to the screen, and returns the character 
     * of the pressed key.
     */
    function char readChar() {
        var char c;
        do Output.printChar(0); // print cursor

        while (key = 0) {
            let key = Keyboard.keyPressed();
        }

        let c = key;

        while (~(key = 0)) {
            let key = Keyboard.keyPressed();
        }


        if (~((c = String.newLine()) | (c = String.backSpace()))) {
            do Output.backSpace(); // overwrite cursor
            do Output.printChar(c); // replace by char - will advance cursor position
        }

        /* 
            if back space
                F#_
                move back twice ?
        
        */

        return c;
    }

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its value. Also handles user backspaces.
     */
    function String readLine(String message) {
        var String line;
        var char c;
        do Output.printString(message);

        let line = String.new(10);

        while (true) {
            let c = Keyboard.readChar();

            if (c = String.newLine()) {
                do Output.backSpace(); // return on displayed cursor
                do Output.printChar(32); // print space = erase cursor
                do Output.println();
                return line;
            }

            if (c = String.backSpace()) {
                // really weird - see if can reduce calls
                do line.eraseLastChar();
                do Output.backSpace(); // return on displayed cursor
                do Output.backSpace(); // return on char
                do Output.printChar(0); // print cursor and advance
                do Output.printChar(32); // print space = erase old cursor and advance
                do Output.backSpace(); // return on empty space
                do Output.backSpace(); // return on displayed cursor

            } else {
                do line.appendChar(c);
            }
        }

        return line;
    }   

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its integer value (until the first non-digit character in the
     * entered text is detected). Also handles user backspaces. 
     */
    function int readInt(String message) {
        var String s;
        let s = Keyboard.readLine(message);
        return s.intValue();
    }
}
