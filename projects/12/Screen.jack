// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Screen.jack

/**
 * A library of functions for displaying graphics on the screen.
 * The Hack physical screen consists of 256 rows (indexed 0..255, top to bottom)
 * of 512 pixels each (indexed 0..511, left to right). The top left pixel on 
 * the screen is indexed (0,0).
 */
class Screen {

    static int color;

    static Array powersOfTwo;

    /** Initializes the Screen. */
    function void init() {
        let color = -1;

        // for mask

        /* 
        mask = 0000 0000 0001 0000
        ~mask = 1111 1111 1110 1111

        masked = value & ~mask = bit set to 0 in mask sets the bit in value to 0 and the rest is unchanged

        color | masked = 0000 0000 0000 0000 | masked sets the masked bit to 0
                        OR 1111 1111 1111 1111 | masked sets the masked bit to 1


        screen space from 16384 to 32727

        each memory entry is 16 bit-wide
        256 rows and 512 columns

        row length in addresses = number of columns / word-size = 512 / 16 = 32 addresses

        get corresponding start address for a column
            integer division of column by a word = x / 16

        all in all base address for (x, y) = row offset + column offset

        quotient = (x / 16)
        offset = (32 * y) + quotient
        addr = base addr + offset 
             = 16384 + offset

             value = peek word at address

        remainder = value - (quotient * 16)

        indexPowerOfTwo = 16 - remainder - 1

        mask = powersOfTwo[indexPowerOfTwo]

        color | (value & ~mask)
             
        */
        let powersOfTwo = Array.new(16);
        let powersOfTwo[0] = 1;
        let powersOfTwo[1] = 2;
        let powersOfTwo[2] = 4;
        let powersOfTwo[3] = 8;
        let powersOfTwo[4] = 16;
        let powersOfTwo[5] = 32;
        let powersOfTwo[6] = 64;
        let powersOfTwo[7] = 128;
        let powersOfTwo[8] = 256;
        let powersOfTwo[9] = 512;
        let powersOfTwo[10] = 1024;
        let powersOfTwo[11] = 2048;
        let powersOfTwo[12] = 4096;
        let powersOfTwo[13] = 8192;
        let powersOfTwo[14] = 16384;
        let powersOfTwo[15] = 32767 + 1; // else error Integer is too big = -32768 in 2's complement
        return;
    }

    /** Erases the entire screen. */
    function void clearScreen() {
        var int addr;

        let addr = 16384;
        while (~(addr > 32727)) {
            do Memory.poke(addr, 0);
            let addr = addr + 1;
        }

        return;
    }

    /** Sets the current color, to be used for all subsequent drawXXX commands.
     *  Black is represented by true, white by false. */
    function void setColor(boolean b) {
        if (b) {

            let color = -1;
        } else {
            let color = 0;
        }
        return;
    }

    /** Draws the (x,y) pixel, using the current color. */
    function void drawPixel(int x, int y) {
        var int addr, quotient, remainder, value, mask;

        let quotient = x / 16; // division entiere

        let addr = 16384 + (32 * y) + quotient;

        let value = Memory.peek(addr);

        // reste  de la division x / 16
        let remainder = x - (quotient * 16);

        // !!!!! IMPORTANT !!!!!
        // pour un word à une adresse donnée, les bits de poids faible correspondent aux pixels les plus à gauche
        /* 
            ( B stands for black , which is true bit, W for white):

            BBBBBWWWWWWWWWWW

            you actually have to write the  16-bit  binary value as :

            0000000000011111
        

            il suffit donc d'utiliser le reste de la division pour connaitre l'index de la puissance de 2 voulue
        */

        let mask = powersOfTwo[remainder];


        /* 
         (color & mask) = ne garde que la couleur pour le bit a appliquer
         (value & (~mask)) = set a 0 le bit à colorer et conserve les autres intacts
         (value & (~mask)) | (color & mask) = applique la couleur garder au bit à colorer uniquement
        */
        let value = (value & (~mask)) | (color & mask);

        do Memory.poke(addr, value);

        return;
    }

    /** Draws a line from pixel (x1,y1) to pixel (x2,y2), using the current color. */
    function void drawLine(int x1, int y1, int x2, int y2) {

        /*
            Origin is different from example

            example, origin is bottom-left

            implementation, origin is top-left 
        */

        var int a, b, diff, dx, dy, xLeft, xRight;

        var int yTop, yBottom;

        var int yLeft, yRight;
        

        if (y1 = y2) { // horizontal
            let xLeft = Math.min(x1, x2);
            let xRight = Math.max(x1, x2);


            while (~(xLeft > xRight)) {
                do Screen.drawPixel(xLeft, y1);

                let xLeft = xLeft + 1;
            }

            return;
        }

        if (x1 = x2) { // vertical

            let yTop = Math.min(y1, y2);
            let yBottom = Math.max(y1, y2);


            while (~(yTop > yBottom)) {
                do Screen.drawPixel(x1, yTop);

                let yTop = yTop + 1;
            }

            return;


        }

        let a = 0;
        let b = 0;
        let diff = 0;
        let dx = Math.abs(x2 - x1);
        let dy = Math.abs(y2 - y1);

        /* special cases compare for horizontal of vertical line

        horizontal y1 == y2
        vertical x1 == x2

        else there is a slope between points

        reduce the problem to drawing a line going north-east or south-east

        find left-most point
        xLeft, yLeft
        xRight, yRight

        direction of slope

        upward
        yLeft < yRight

        downward
        yLeft > yRight

        */

        if (x1 < x2) {
            let xLeft = x1;
            let yLeft = y1;

            let xRight = x2;
            let yRight = y2;

        } else {
            let xLeft = x2;
            let yLeft = y2;

            let xRight = x1;
            let yRight = y1;
        }

         while ( (~(a > dx)) & (~(b > dy)) ) {
            
            if (yLeft > yRight) { // TODO: avoid check every time - call deterministic function
                do Screen.drawPixel(xLeft + a, yLeft - b); // go north east
            } else {
                do Screen.drawPixel(xLeft + a, yLeft + b); // go south east
            }

            /*
            slope comparison b/a > dy/dx (= overshooting the correct slope => correct by increasing a by 1)
                <=> a*dy - b*dx < 0


            slope comparison b/a <= dy/dx (= undershooting the correct slope => correct by increasing b by 1)
                <=> a*dy - b*dx >= 0

                let diff = a*dy - b*dx

                diff(a + 1, b) = diff(a, b) + dy

                diff(a, b + 1) = diff(a, b) - dx

            */  
            if (diff < 0) { // go right
                let a = a + 1;
                let diff = diff + dy;
            } else { // go up - always begin by going up
                let b = b + 1;
                let diff = diff - dx;
            }
        }

        return;

    }

    /** Draws a filled rectangle whose top left corner is (x1, y1)
     * and bottom right corner is (x2,y2), using the current color. */
    function void drawRectangle(int x1, int y1, int x2, int y2) {

        while (~(y1 > y2)) {

            do Screen.drawLine(x1, y1, x2, y1);
            let y1 = y1 + 1;
        }

         return;
    }

    /** Draws a filled circle of radius r<=181 around (x,y), using the current color. */
    function void drawCircle(int x, int y, int r) {
        /* 
        for each dy = –r to r do:

            drawLine (x - sqrt(r^2 - dy^2), y + dy,
                      x + sqrt(r^2 - dy^2), y + dy)
        */

        var int dy, root;
        let dy = -r;

        while (~(dy > r)) {

            let root = Math.sqrt(Math.multiply(r, r) - Math.multiply(dy, dy));

            do Screen.drawLine(x - root, y + dy, x + root, y + dy);

            let dy = dy + 1;
        }
         return;
    }
}
